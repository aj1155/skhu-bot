buildscript {
	ext {
		springBootVersion = '1.5.2.RELEASE'
	}
	repositories {
		mavenCentral()
	}
	dependencies {
		classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
	}
}

apply plugin: 'war'
apply plugin: 'org.springframework.boot'
apply plugin: 'idea'

jar {
	baseName = 'skhu-bot'
	version = '0.0.1-SNAPSHOT'
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
	mavenCentral()
}


dependencies {
	compile('org.springframework.boot:spring-boot-starter-data-jpa')
	compile('org.springframework.boot:spring-boot-starter-web')
	compile("joda-time:joda-time")
	compile("org.jadira.usertype:usertype.jodatime:2.0.1")
	compile("com.google.guava:guava:20.0")
	compile("org.projectlombok:lombok:1.16.2")
	compile("com.google.code.gson:gson:2.8.0")
	compile "org.igniterealtime.smack:smack-core:4.0.4"
	compile "org.igniterealtime.smack:smack-tcp:4.0.4"
	compile "com.googlecode.json-simple:json-simple:1.1"
	compile('eu.bitwalker:UserAgentUtils:1.20')
	compile('org.apache.commons:commons-lang3:3.0')
	// https://mvnrepository.com/artifact/com.twitter.penguin/korean-text
	compile group: 'com.twitter.penguin', name: 'korean-text', version: '4.4'
	runtime('com.h2database:h2:1.4.186')
	runtime('mysql:mysql-connector-java:5.1.35')
	testCompile('org.springframework.boot:spring-boot-starter-test')

	def querydslVersion = "4.1.4"

	compile ("com.querydsl:querydsl-core:$querydslVersion")
	compile ("com.querydsl:querydsl-apt:$querydslVersion")
	compile ("com.querydsl:querydsl-jpa:$querydslVersion")
}

[compileJava,compileTestJava]*.options*.encoding = 'UTF-8'

sourceSets{
	generated {
		java { srcDirs = ['src/main/generated/java']}
	}
	main {
		java {
			srcDirs 'src/main/java'
			srcDirs 'src/main/generated/java'
		}
		resources { srcDir 'src/main/resources' }
	}
	test {
		java { srcDir 'src/test/java' }
		resources { srcDir 'src/test/resources' }
	}
}

def queryDslOutput = file("src/main/generated/java")

task generateQueryDSL(type: JavaCompile,group: 'build'){
	doFirst {
		if(!queryDslOutput.exists()){
			logger.info("Creating `$queryDslOutput` directory")

			if(!queryDslOutput.mkdirs()){
				throw new InvalidUserDataException("Unable to Create `$queryDslOutput` directory")
			}
		}else{
			FileCollection collection = files { file(queryDslOutput).listFiles() }
			collection.each { delete it }
		}
	}

	source = sourceSets.main.java
	classpath = configurations.compile
	options.compilerArgs = [
			"-proc:only",
			"-processor",
			"com.querydsl.apt.jpa.JPAAnnotationProcessor"
	]
	destinationDir = queryDslOutput


}

clean {
	delete queryDslOutput
}

compileTestJava.dependsOn(generateQueryDSL)
